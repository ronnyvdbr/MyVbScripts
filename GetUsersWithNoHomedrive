option explicit
'********************************************************************************************
'********************* OPTION EXPLICIT = ALLE VARIABELEN DEFINIEREN *************************
'********************************************************************************************

Dim objRootDSE, strDNSDomain, objCommand, objConnection, objFSO
Dim strBase, strFilter, strAttributes, strQuery, objRecordSet
Dim strDN, objfolder, logstring, RootPath, LogPath, Logfile


RootPath = "\\ecu\dfs001\homedirs"
LogPath = "C:\Documents and Settings\ronnyvdb\Desktop\logfile.txt"


on error resume next

Set objFSO = CreateObject("Scripting.FileSystemObject")

' Determine DNS domain name.
Set objRootDSE = GetObject("LDAP://RootDSE")
strDNSDomain = objRootDSE.Get("defaultNamingContext")

' Use ADO to search Active Directory.
Set objCommand = CreateObject("ADODB.Command")
Set objConnection = CreateObject("ADODB.Connection")
objConnection.Provider = "ADsDSOObject"
objConnection.Open "Active Directory Provider"
objCommand.ActiveConnection = objConnection

' Specify the base of the search.
strBase = "<LDAP://" & strDNSDomain & ">"

' Filter on all user objects in the domain.
strFilter = "(& (objectCategory=person)(objectClass=user))"

' Specify the attributes to retrieve (comma delimited list).
strAttributes = "SamAccountName"

' Specify the query. "subtree" means to search all child containers/OU's.
strQuery = strBase & ";" & strFilter & ";" & strAttributes & ";subtree"

objCommand.CommandText = strQuery
objCommand.Properties("Page Size") = 100
objCommand.Properties("Timeout") = 30
objCommand.Properties("Cache Results") = False
Set objRecordSet = objCommand.Execute

Do Until objRecordSet.EOF
strDN = objRecordSet.Fields("SamAccountName")


set objfolder = objFSO.GetFolder(RootPath + "\" + strDN)
If Err.number <> 0 Then 

'*************************************************************

Set Logfile = objFSO.OpenTextFile(LogPath, 8, True)
Logfile.WriteLine(strDN)
Logfile.Close

end if

Err.clear

objRecordSet.MoveNext
Loop

' Clean up.
objConnection.Close
Set objRootDSE = Nothing
Set objCommand = Nothing
Set objConnection = Nothing
Set objRecordSet = Nothing
